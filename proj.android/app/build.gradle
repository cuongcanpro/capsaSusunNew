apply plugin: 'com.android.application'
apply from: project(':libaxmol').projectDir.toString() + "/../dsl/axmol.gradle"
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.gms.google-services'
// Resolve build profiles
def buildProfiles = AxmolUtils.resolveBuildProfiles(project)

android {
    def packageName = buildProfiles['packageName']
    def cmakeVer = buildProfiles['cmakeVer']
    def cmakeOptions = Eval.me(buildProfiles['cmakeOptions'])
    def minSdk = buildProfiles['minSdk']
    def targetSdk = buildProfiles['targetSdk']

    // Apply build profiles
    namespace = packageName
    compileSdk = targetSdk.toInteger()
    ndkVersion = "27.3.13750724"
    if(buildProfiles.containsKey('ndkPath')) {
        ndkPath = buildProfiles['ndkPath']
    }

    defaultConfig {
        applicationId packageName
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode 5
        versionName "1.0"

        externalNativeBuild {
            cmake {
                arguments = []
                arguments.addAll(cmakeOptions)
                cppFlags "-frtti -fexceptions -fsigned-char"
            }
        }

        ndk {
            // noinspection ChromeOsAbiSupport
            abiFilters = __1K_ARCHS.split(':').collect{it as String}
        }
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "build/assets"
    }

    externalNativeBuild {
        cmake {
            version = cmakeVer
            path "../../CMakeLists.txt"
        }
    }

    signingConfigs {
       release {
            if (project.hasProperty("KEY_STORE_FILE")) {
                storeFile file(KEY_STORE_FILE)
                storePassword KEY_STORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false
            shrinkResources = false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFile file(project(':libaxmol').projectDir.toString() + '/proguard-rules.pro')
            if (project.hasProperty("KEY_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            debuggable true
            jniDebuggable true
        }
    }

    aaptOptions {
       noCompress 'mp3','ogg','wav','mp4','ttf','ttc','otf'
    }

    buildFeatures {
        aidl = true
    }
}

android.applicationVariants.configureEach { variant ->
    def variantName = variant.name.capitalize()
    tasks.register("copy${variantName}ContentToAssets") {
        doFirst {
            delete "${projectDir}/build/assets"
        }
        doLast {
            copy {
                from "${projectDir}/../../Content"
                into "${projectDir}/build/assets"
                exclude "**/*.gz"
            }
            copy {
                from "${projectDir}/build/runtime/axslc"
                into "${projectDir}/build/assets/axslc"
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':libaxmol')
    implementation 'com.google.android.gms:play-services-ads:23.2.0'
    implementation("com.unity3d.ads:unity-ads:4.10.0")
    implementation("com.google.ads.mediation:unity:4.10.0.0")
    // Import the BoM for the Firebase platform
    implementation(platform("com.google.firebase:firebase-bom:33.2.0"))
    // Add the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation("com.google.firebase:firebase-crashlytics")
    implementation("com.google.firebase:firebase-analytics")
    implementation "com.google.android.gms:play-services-games-v2:20.1.2"

    implementation("com.google.ads.mediation:facebook:6.17.0.0")
    implementation 'com.facebook.android:audience-network-sdk:6.17.0'
    implementation 'com.facebook.infer.annotation:infer-annotation:0.18.0'
    implementation 'com.android.installreferrer:installreferrer:2.2'
}

project.afterEvaluate {
    android.applicationVariants.configureEach { variant ->
        def variantName = variant.name.capitalize()
        def copyTask = tasks.named("copy${variantName}ContentToAssets")

        tasks.named("merge${variantName}Assets").configure {
            dependsOn(copyTask)
        }
    }
}
